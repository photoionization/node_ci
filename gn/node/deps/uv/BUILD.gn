# Copyright (c) 2013-2019 GitHub Inc.
# Copyright 2019 the V8 project authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//node/node.gni")

if (is_fuchsia) {
  import("//third_party/fuchsia-sdk/sdk/build/component.gni")
  import("//third_party/fuchsia-sdk/sdk/build/package.gni")
}

config("libuv_config") {
  include_dirs = [
    "include",
    "src",
  ]

  defines = []

  if (is_linux) {
    defines += [
      "_POSIX_C_SOURCE=200112",
      "_GNU_SOURCE",
    ]
  }
  if (!is_win) {
    defines += [
      "_LARGEFILE_SOURCE",
      "_FILE_OFFSET_BITS=64",
      "_GNU_SOURCE",

      # This only has an effect on Windows, where it will cause libuv's
      # symbols to be exported in node.lib
      "BUILDING_UV_SHARED=1",
    ]
  }
  if (is_mac) {
    defines += [
      "_DARWIN_USE_64_BIT_INODE=1",
      "_DARWIN_UNLIMITED_SELECT=1",
    ]
  }
}

node_dep("uv") {
  public_configs = [ ":libuv_config" ]
  libs = []
  ldflags = []

  sources = [
    "include/uv.h",
    "include/uv/errno.h",
    "include/uv/threadpool.h",
    "include/uv/tree.h",
    "include/uv/version.h",
    "src/fs-poll.c",
    "src/heap-inl.h",
    "src/idna.c",
    "src/idna.h",
    "src/inet.c",
    "src/queue.h",
    "src/random.c",
    "src/strscpy.c",
    "src/strscpy.h",
    "src/threadpool.c",
    "src/timer.c",
    "src/uv-common.c",
    "src/uv-common.h",
    "src/uv-data-getter-setters.c",
    "src/version.c",
  ]

  if (is_win) {
    sources += [
      "include/uv/win.h",
      "src/win/async.c",
      "src/win/atomicops-inl.h",
      "src/win/core.c",
      "src/win/detect-wakeup.c",
      "src/win/dl.c",
      "src/win/error.c",
      "src/win/fs-event.c",
      "src/win/fs.c",
      "src/win/getaddrinfo.c",
      "src/win/getnameinfo.c",
      "src/win/handle-inl.h",
      "src/win/handle.c",
      "src/win/internal.h",
      "src/win/loop-watcher.c",
      "src/win/pipe.c",
      "src/win/poll.c",
      "src/win/process-stdio.c",
      "src/win/process.c",
      "src/win/req-inl.h",
      "src/win/signal.c",
      "src/win/snprintf.c",
      "src/win/stream-inl.h",
      "src/win/stream.c",
      "src/win/tcp.c",
      "src/win/thread.c",
      "src/win/tty.c",
      "src/win/udp.c",
      "src/win/util.c",
      "src/win/winapi.c",
      "src/win/winapi.h",
      "src/win/winsock.c",
      "src/win/winsock.h",
    ]
    libs += [
      "advapi32.lib",
      "iphlpapi.lib",
      "psapi.lib",
      "shell32.lib",
      "user32.lib",
      "userenv.lib",
      "ws2_32.lib",
    ]
  } else {
    sources += [
      "include/uv/aix.h",
      "include/uv/bsd.h",
      "include/uv/darwin.h",
      "include/uv/linux.h",
      "include/uv/sunos.h",
      "include/uv/unix.h",
      "src/unix/async.c",
      "src/unix/atomic-ops.h",
      "src/unix/core.c",
      "src/unix/dl.c",
      "src/unix/fs.c",
      "src/unix/getaddrinfo.c",
      "src/unix/getnameinfo.c",
      "src/unix/internal.h",
      "src/unix/loop-watcher.c",
      "src/unix/loop.c",
      "src/unix/pipe.c",
      "src/unix/poll.c",
      "src/unix/process.c",
      "src/unix/pthread-fixes.c",
      "src/unix/random-devurandom.c",
      "src/unix/random-getentropy.c",
      "src/unix/random-getrandom.c",
      "src/unix/signal.c",
      "src/unix/spinlock.h",
      "src/unix/stream.c",
      "src/unix/tcp.c",
      "src/unix/thread.c",
      "src/unix/tty.c",
      "src/unix/udp.c",
    ]
    libs += [ "m" ]
    ldflags += [ "-pthread" ]
  }
  if (is_mac || is_linux) {
    sources += [ "src/unix/proctitle.c" ]
  }
  if (is_mac) {
    sources += [
      "src/unix/darwin-proctitle.c",
      "src/unix/darwin.c",
      "src/unix/fsevents.c",
    ]
  }
  if (is_linux) {
    sources += [
      "src/unix/linux-core.c",
      "src/unix/linux-inotify.c",
      "src/unix/linux-syscalls.c",
      "src/unix/linux-syscalls.h",
      "src/unix/procfs-exepath.c",
      "src/unix/random-sysctl-linux.c",
      "src/unix/sysinfo-loadavg.c",
    ]
    libs += [
      "dl",
      "rt",
    ]
  }
  if (is_mac) {  # is_bsd
    sources += [
      "src/unix/bsd-ifaddrs.c",
      "src/unix/kqueue.c",
    ]
  }
  if (is_fuchsia) {
    sources += [
      "//gn/node/deps/uv/src/unix/fuchsia.c",
      "src/unix/no-fsevents.c",
      "src/unix/no-proctitle.c",
      "src/unix/posix-hrtime.c",
      "src/unix/posix-poll.c",
    ]
    deps = [
      "//third_party/fuchsia-sdk/sdk/pkg/fdio",
    ]
  }
}

source_set("test-close-fd") {
  cflags_c = [
    "-Wno-string-conversion",
  ]
  sources = [
    "test/test-close-fd.c",
  ]
  deps = [
    ":uv",
  ]
}

source_set("test-tty") {
  cflags_c = [
    "-Wno-string-conversion",
  ]
  sources = [
    "test/test-tty-duplicate-key.c",
    "test/test-tty.c",
  ]
  deps = [
    ":uv",
  ]
}

executable("uv_tests") {
  sources = [
    "test/blackhole-server.c",
    "test/dns-server.c",
    "test/echo-server.c",
    "test/fixtures",
    "test/run-tests.c",
    "test/runner-unix.c",
    "test/runner-unix.h",
    "test/runner.c",
    "test/runner.h",
    "test/task.h",
    "test/test-active.c",
    "test/test-async-null-cb.c",
    "test/test-async.c",
    "test/test-barrier.c",
    "test/test-callback-order.c",
    "test/test-callback-stack.c",
    "test/test-close-order.c",
    "test/test-condvar.c",
    "test/test-connect-unspecified.c",
    "test/test-connection-fail.c",
    "test/test-cwd-and-chdir.c",
    "test/test-default-loop-close.c",
    "test/test-delayed-accept.c",
    "test/test-dlerror.c",
    "test/test-eintr-handling.c",
    "test/test-embed.c",
    "test/test-env-vars.c",
    "test/test-error.c",
    "test/test-fail-always.c",
    "test/test-fork.c",
    "test/test-fs-copyfile.c",
    "test/test-fs-event.c",
    "test/test-fs-fd-hash.c",
    "test/test-fs-open-flags.c",
    "test/test-fs-poll.c",
    "test/test-fs-readdir.c",
    "test/test-fs.c",
    "test/test-get-currentexe.c",
    "test/test-get-loadavg.c",
    "test/test-get-memory.c",
    "test/test-get-passwd.c",
    "test/test-getaddrinfo.c",
    "test/test-gethostname.c",
    "test/test-getnameinfo.c",
    "test/test-getsockname.c",
    "test/test-getters-setters.c",
    "test/test-gettimeofday.c",
    "test/test-handle-fileno.c",
    "test/test-homedir.c",
    "test/test-hrtime.c",
    "test/test-idle.c",
    "test/test-ip4-addr.c",
    "test/test-ip6-addr.c",
    "test/test-ipc-heavy-traffic-deadlock-bug.c",
    "test/test-ipc-send-recv.c",
    "test/test-ipc.c",
    "test/test-list.h",
    "test/test-loop-alive.c",
    "test/test-loop-close.c",
    "test/test-loop-configure.c",
    "test/test-loop-handles.c",
    "test/test-loop-stop.c",
    "test/test-loop-time.c",
    "test/test-multiple-listen.c",
    "test/test-mutexes.c",
    "test/test-osx-select.c",
    "test/test-pass-always.c",
    "test/test-ping-pong.c",
    "test/test-pipe-bind-error.c",
    "test/test-pipe-close-stdout-read-stdin.c",
    "test/test-pipe-connect-error.c",
    "test/test-pipe-connect-multiple.c",
    "test/test-pipe-connect-prepare.c",
    "test/test-pipe-getsockname.c",
    "test/test-pipe-pending-instances.c",
    "test/test-pipe-sendmsg.c",
    "test/test-pipe-server-close.c",
    "test/test-pipe-set-fchmod.c",
    "test/test-pipe-set-non-blocking.c",
    "test/test-platform-output.c",
    "test/test-poll-close-doesnt-corrupt-stack.c",
    "test/test-poll-close.c",
    "test/test-poll-closesocket.c",
    "test/test-poll-oob.c",
    "test/test-poll.c",
    "test/test-process-priority",
    "test/test-process-title-threadsafe.c",
    "test/test-process-title.c",
    "test/test-queue-foreach-delete.c",
    "test/test-random.c",
    "test/test-ref.c",
    "test/test-run-nowait.c",
    "test/test-run-once.c",
    "test/test-semaphore.c",
    "test/test-shutdown-close.c",
    "test/test-shutdown-eof.c",
    "test/test-shutdown-twice.c",
    "test/test-signal-multiple-loops.c",
    "test/test-signal-pending-on-close.c",
    "test/test-signal.c",
    "test/test-socket-buffer-size.c",
    "test/test-spawn.c",
    "test/test-stdio-over-pipes.c",
    "test/test-tcp-alloc-cb-fail.c",
    "test/test-tcp-bind-error.c",
    "test/test-tcp-bind6-error.c",
    "test/test-tcp-close-accept.c",
    "test/test-tcp-close-reset.c",
    "test/test-tcp-close-while-connecting.c",
    "test/test-tcp-close.c",
    "test/test-tcp-connect-error-after-write.c",
    "test/test-tcp-connect-error.c",
    "test/test-tcp-connect-timeout.c",
    "test/test-tcp-connect6-error.c",
    "test/test-tcp-create-socket-early.c",
    "test/test-tcp-flags.c",
    "test/test-tcp-oob.c",
    "test/test-tcp-open.c",
    "test/test-tcp-read-stop.c",
    "test/test-tcp-shutdown-after-write.c",
    "test/test-tcp-try-write-error.c",
    "test/test-tcp-try-write.c",
    "test/test-tcp-unexpected-read.c",
    "test/test-tcp-write-after-connect.c",
    "test/test-tcp-write-fail.c",
    "test/test-tcp-write-queue-order.c",
    "test/test-tcp-write-to-half-open-connection.c",
    "test/test-tcp-writealot.c",
    "test/test-thread-equal.c",
    "test/test-thread.c",
    "test/test-threadpool-cancel.c",
    "test/test-threadpool.c",
    "test/test-timer-again.c",
    "test/test-timer-from-check.c",
    "test/test-timer.c",
    "test/test-tmpdir.c",
    "test/test-udp-alloc-cb-fail.c",
    "test/test-udp-bind.c",
    "test/test-udp-connect.c",
    "test/test-udp-create-socket-early.c",
    "test/test-udp-dgram-too-big.c",
    "test/test-udp-ipv6.c",
    "test/test-udp-multicast-interface.c",
    "test/test-udp-multicast-interface6.c",
    "test/test-udp-multicast-join.c",
    "test/test-udp-multicast-join6.c",
    "test/test-udp-multicast-ttl.c",
    "test/test-udp-open.c",
    "test/test-udp-options.c",
    "test/test-udp-send-and-recv.c",
    "test/test-udp-send-hang-loop.c",
    "test/test-udp-send-immediate.c",
    "test/test-udp-send-unreachable.c",
    "test/test-udp-try-send.c",
    "test/test-uname.c",
    "test/test-walk-handles.c",
    "test/test.gyp",
  ]

  deps = [
    ":uv",
    ":test-tty",
    ":test-close-fd",
  ]

  if (is_fuchsia) {
    deps += [ "//third_party/fuchsia-sdk/sdk/pkg/fdio" ]
  }

  if (is_linux) {
    sources += [
      "test/test-emfile.c",
      "test/test-idna.c",
      "test/test-strscpy.c",
      "test/test-watcher-cross-stop.c",
      "test/test-process-priority.c",
    ]

    libs = [ "util" ]
  }
}

if (is_fuchsia) {
  fuchsia_component("uv_tests_cmx") {
    manifest = "//gn/node/deps/uv/meta/uv_tests.cmx"
    data_deps = [
      ":uv_tests",
    ]
  }

  fuchsia_package("uv_tests_package") {
    package_name = "uv_tests"
    deps = [
      ":uv_tests_cmx",
    ]
  }
}
